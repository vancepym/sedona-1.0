//
// Copyright (c) 2010 Tridium, Inc
// Licensed under the Academic Free License version 3.0
//
// History:
//   07 Jun 2010  Clif Turman  Creation
//

**
** Avg Running Average math object
**
**   out = (total + in - avg)/n
** 
**   where total = running total of last n samples
**         avg   = last computed average = total/n 
**
**   Note: 1) the "avg" value is "seeded" with the "in" 
**         value at startup or whenever "n" changes
**         2) the "total" value is "seeded" with (in * n)
**         at startup or whenever "n" changes
**         3) boolean input "reset" resets the object to 
**            return to first execute cycle state, and avg
**            and total are re "seeded".  This may be useful
**            to invoke in an application where large step changes
**            of "in" are expected and "n" is a high value, to prevent
**            output lag of the input.
**            
**
**   For large step changes in values, the "out" value will asymptotically
**   approach the "in" value.
**
@niagaraIcon="module://icons/x16/control/math/average.png"
class AvgN
  extends Component
{
  @readonly property float out
  property float in
  
  @min=1
  @config property int numSamplesToAvg = 5

  @readonly property bool reset = false

  **
  ** Callback when component is first started in an app,
  ** before the first call to propagateLinksTo() or execute().
  **
  virtual override void start()
  {
    first = true
  }


  **
  ** Behavior to execute when a property is changed externally
  ** 
  virtual override void changed(Slot slot)
  {
    if(slot == AvgN.numSamplesToAvg)
      first = true
    else if(slot == AvgN.reset)
      if(reset)
        first = true
    super.changed(slot)
  }

  virtual override void execute()
  {
    computeAverage()
    out = avg
  }

  void computeAverage()
  {
    if(first)
    {
      avg = in
      total = (in * (float)numSamplesToAvg)
      first = false
    }
    else
    {
      total = total + in - avg
      avg   = total/(float)numSamplesToAvg
    }
    //Sys.out.print("$in,$avg")
  }


  private float  total
  private float  avg
  private bool   first

}
