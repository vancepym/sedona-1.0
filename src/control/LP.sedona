//
// Copyright (c) 2007 Tridium, Inc
// Licensed under the Academic Free License version 3.0
//
// History:
//   10 Jan 07  Andy Saunders  Creation
//   27 Apr 07  Brian Frank    Port from Java to Sedona
//   22 Apr 09  Elizabeth McKenney   Updated
//

**
** Pid Loop object
**
@niagaraIcon="module://icons/x16/control/numericPoint.png"
class LP
  extends Component
{
  @readonly property float out

  @config property bool enable = true
  @config @summary property float sp    = 0.0f
  @config @summary property float cv    = 0.0f
  @config @precision=4 property float kp = 1.0f
  @config @precision=4 property float ki = 0.0f  // units repeats / minute
  @config @precision=4 property float kd = 0.0f
  @config property float max   = 10.0f
  @config property float min   = 0.0f
  @config property float bias  = 0.0f
  @config property bool direct = true 
  @config @unit=Units.millisecond property int exTime  = 10

  **
  ** Callback when component is first started in an app.
  **
  virtual override void start()
  {
    executeTime = (long)exTime * 1ms
    kPkIconst   = kp * ki / 60.0f
    if (kPkIconst==0.0f)
    {
      scaledMax = 0.0f
      scaledMin = 0.0f
    }
    else
    {
      scaledMax = max / kPkIconst
      scaledMin = min / kPkIconst
    }
  }


  **
  ** Execute is called once every scan.
  **
  virtual override void execute()
  {
    if (!enable) return

    long now = Sys.ticks()
        
    // return if not time to execute
    //long executeTime = (1ms *  (long)exTime)
    long deltaTime = now - lastExecTime
    if ( deltaTime < executeTime)
      return;

    lastExecTime = now

    float kProportional = kp 
    float kIntegral     = ki
    float kDerivative   = kd 
    //float kPkIconst     = kProportional * kIntegral / 60.0f

    if (kProportional == 0.0f)
      return;

    // TODO check for valid setpoint and cv

    // calculate execution time as a float in seconds
    float deltaSecs = (float)deltaTime / float1sec

    //  Calculate the current error
    float error = sp - cv

    //  Accumulate the error for integral control
    if (kIntegral != 0.0)
    {
      float iError = deltaSecs * error
      errorSum += iError

      // Constrain the error sum to prevent integral windup
      if (direct)
      {
        if (-errorSum > scaledMax)
          errorSum = -scaledMax
        else if (-errorSum < scaledMin)
          errorSum = -scaledMin
      }
      else
      {
        if (errorSum > scaledMax)
          errorSum = scaledMax
        else if (errorSum < scaledMin)
          errorSum = scaledMin
      }
    }

    //  Calculate the proportional gain
    float proportionalGain = error * kProportional

    //  Calculate the integral gain
    //  All gain values are calculated in seconds.  Convert integral
    //  term (resets per minute) to resets per second
    float integralGain = kProportional * kIntegral * errorSum / 60.0f


    //  Calculate the derivative gain
    float derivativeGain = kProportional * kDerivative * (error - lastError) / deltaSecs

    //  Store the last error for derivative gain
    lastError = error

    //  Calculate the overall gain
    float pv = proportionalGain + integralGain + derivativeGain

    //  Apply action
    if ( direct )
      pv = - pv;

    //  Add bias if not PI
    if (kIntegral == 0.0f)
      pv = pv + bias

    // Constrain the overall gain
    if (pv > max)
      pv = max
    else if (pv < min)
      pv = min

    out = pv
  }


  **
  ** Behavior to execute when a property is changed externally
  ** 
  override void changed(Slot slot)
  {
    super.changed(slot)

    if (slot == LP.exTime)
      executeTime = (1ms *  (long)exTime)
    
    if ((slot == LP.kp) || (slot == LP.ki))
      kPkIconst = kp * ki / 60.0f

    if (slot == LP.max)
    {
      if (kPkIconst==0.0f)
        scaledMax = 0.0f
      else
        scaledMax = max / kPkIconst
    }

    if (slot == LP.min)
    {
      if (kPkIconst==0.0f)
        scaledMin = 0.0f
      else
        scaledMin = min / kPkIconst
    }
  }


  // Precalc seldom-changed values for better performance
  private long  executeTime
  private float kPkIconst
  private float scaledMax
  private float scaledMin

  private define float float1sec = (float)1sec
  
  // Values cached between executions
  private long  lastExecTime = 0L
  private float errorSum     = 0.0
  private float lastError    = 0.0
}
