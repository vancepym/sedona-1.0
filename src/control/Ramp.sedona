//
// Copyright (c) 2007 Tridium, Inc
// Licensed under the Academic Free License version 3.0
//
// History:
//   26 Oct 07  Brian Frank    Creation
//   27 Apr 07  Brian Frank    Port from Java to Sedona
//

**
** Ramp
**   Triangle wave between min & max
**   Slope is fixed: 1.0 unit per App cycle
**
@niagaraIcon="module://icons/x16/control/util/sine.png"
class Ramp
  extends Component
{
  @readonly property float out
  @config property float min   = 0.0
  @config property float max   = 100.0
  @config property float delta = 1.0

  virtual override void execute()
  {
    // Don't update more than 4x/sec
    long now = Sys.ticks()
    if (now - lastUpdate < 250ms) return

    if (firstHalf)
      out = out + delta
    else
      out = out - delta

    if (out < min) { out = min; firstHalf = true }
    if (out > max) { out = max; firstHalf = false }
  }

  private bool firstHalf = true
  private long lastUpdate
}
