//
// Copyright (c) 2007 Tridium, Inc
// Licensed under the Academic Free License version 3.0
//
// History:
//   21 Aug 06  Andy Saunders  Creation
//   27 Apr 07  Brian Frank    Port from Java to Sedona
//   16 May 07  Andy Saunders  completed required time processing
//   22 Apr 09  Elizabeth McKenney   Simplified logic
//

**
** TickTock component
**   Generates boolean square wave with ticksPerSec pulses per second
**
@niagaraIcon="module://icons/x16/control/util/sine.png"
class TickTock
  extends Component
{
  @readonly property bool out
  @config @min=1 @max=10 property int ticksPerSec = 1 

  **
  ** Callback when component is first started in an app.
  **
  virtual override void start()
  {
    halfPeriod = 500ms / (long)ticksPerSec
  }


  **
  ** Execute is called once every scan.
  **
  virtual override void execute()
  {
    // Assume waveform began at time==0, and determine which half of the cycle 
    // we are in now.  If first half, set out=true; if second half, set out=false.

    out = ((Sys.ticks() / halfPeriod) % 2L) == 0L
  }


  **
  ** Behavior to execute when a property is changed externally
  ** 
  override void changed(Slot slot)
  {
    super.changed(slot)

    if (slot == TickTock.ticksPerSec)
      halfPeriod = 500ms / (long)ticksPerSec
  }


  private long halfPeriod   // cached value of 1/2 period, avoids calculating every cycle
}
